plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdk 33

    defaultConfig {
        applicationId "com.mateusrodcosta.apps.share2storage"
        minSdk 23
        targetSdk 32
        versionCode 5
        versionName "0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion = '1.3.0'
    }
    namespace 'com.mateusrodcosta.apps.share2storage'
}

dependencies {
    implementation platform('androidx.compose:compose-bom:2022.12.00')

    implementation "androidx.compose.material3:material3"

    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation'

    implementation 'androidx.compose.ui:ui'

    // Tooling support (Previews, etc.)
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"

    // Material design icons
    implementation 'androidx.compose.material:material-icons-core'
    implementation 'androidx.compose.material:material-icons-extended'

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.6.1'

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.3.3'

    testImplementation 'junit:junit:4.13.2'
}