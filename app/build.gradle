plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

def keystorePropertiesFile = rootProject.file("key.properties")

def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

kotlin {
    jvmToolchain 17
}

android {
    namespace 'com.mateusrodcosta.apps.share2storage'

    compileSdk 34
    defaultConfig {
        applicationId "com.mateusrodcosta.apps.share2storage"
        minSdk 26
        targetSdk 33
        versionCode 11
        versionName "0.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.3"
    }

}

dependencies {
    def composeBom = platform(libs.androidx.compose.bom)
    implementation(composeBom)
    androidTestImplementation(composeBom)

    implementation libs.bundles.compose
    debugImplementation libs.bundles.compose.debug

    implementation libs.androidx.activity.compose

    testImplementation libs.bundles.testing
    androidTestImplementation libs.bundles.ui.testing
}